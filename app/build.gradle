apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion rootProject.ext.versions.compile_sdk
    defaultConfig {
        applicationId "com.yblxt.sugar"
        minSdkVersion rootProject.ext.versions.min_sdk
        targetSdkVersion rootProject.ext.versions.target_sdk
        versionCode getCustomVersion("code")
        versionName getCustomVersion("name")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

// version name format
// main.code.sha1.date
static def getCustomVersion(type) {
    def main = "1.0.0"
    def process = "git rev-list --count HEAD".execute()
    process.waitFor()
    def code = process.text.toInteger()

    if ("code" == type) {
        code
    } else if ("name" == type) {
        process = "git describe --always".execute()
        process.waitFor()
        def sha1 = process.text.trim()
        def date = new Date().format("yyyyMMddHHmmss")
        "$main.$code.$sha1.$date"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    api project(path: ':jetpack')

    def deps = rootProject.ext.deps
    implementation deps.kotlin.stdlib
    implementation deps.androidx.appcompat
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.recyclerview
    // rxlifecycle
    implementation deps.rxlifecycle.android_lifecycle_kotlin
    // dagger
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_compiler
    // glide
    implementation deps.glide.runtime
    kapt deps.glide.compiler
    // retrofit
    implementation deps.retrofit.runtime
    implementation deps.retrofit.converter_gson
    implementation deps.retrofit.adapter_rxjava2
}
